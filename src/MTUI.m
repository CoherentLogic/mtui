MTUI(APPKEY,APPNAME) 
 S MTUIAPP=APPKEY
 S ^%MTUI(MTUIAPP)=APPNAME
 Q
 ;;
INIT
 D INIT^%TERMIO
 S G0=$C(27)_"(B"
 S G1=$C(27)_"(0"
 S BOX("UL")=G1_"l"_G0,BOX("UR")=G1_"k"_G0,BOX("LL")=G1_"m"_G0,BOX("LR")=G1_"j"_G0
 S BOX("LT")=G1_"t"_G0,BOX("RT")=G1_"u"_G0,BOX("V")=G1_"x"_G0,BOX("H")=G1_"q"_G0
 S BOX("X")=G1_"n"_G0,BOX("TT")=G1_"w"_G0,BOX("BT")=G1_"v"_G0
 S SHADOW=G1_"a"_G0
 S SCRAT("RESET")=0
 S SCRAT("BRIGHT")=1
 S SCRAT("DIM")=2
 S SCRAT("UNDERSCORE")=4
 S SCRAT("BLINK")=5
 S SCRAT("REVERSE")=7
 S SCRAT("HIDDEN")=8
 S SCRAT("BLACK")=30
 S SCRAT("RED")=31
 S SCRAT("GREEN")=32
 S SCRAT("YELLOW")=33
 S SCRAT("BLUE")=34
 S SCRAT("MAGENTA")=35
 S SCRAT("CYAN")=36
 S SCRAT("WHITE")=37
 U $P:(CONVERT:NOWRAP)
 D NOSCROLLREGION^MTUIUTIL
 D SETCOLOR^MTUIUTIL("WHITE","BLACK")
 D CLEAR^MTUIUTIL
 D SCROLLREGION^MTUIUTIL(3,%IOCAP("ROWS")-2)
 D SETATTR^MTUIUTIL("RESET")
 D LOCATE^MTUIUTIL(1,1)
 D SETATTR^MTUIUTIL("BRIGHT")
 D SETCOLOR^MTUIUTIL("CYAN","BLACK")
 W ^%MTUI(MTUIAPP)
 D SETATTR^MTUIUTIL("RESET")
 D SETCOLOR^MTUIUTIL("GREEN","BLACK")
 D LOCATE^MTUIUTIL(1,%IOCAP("COLUMNS")-11)
 W $G(TTUSER,"NO USER")
 D SETCOLOR^MTUIUTIL("GREEN","BLACK")
 D LOCATE^MTUIUTIL(%IOCAP("ROWS")-1,1)
 F I=1:1:%IOCAP("COLUMNS") D LOCATE^MTUIUTIL(2,I) W BOX("H")
 D LOCATE^MTUIUTIL(23,1)
 F I=1:1:%IOCAP("COLUMNS") D LOCATE^MTUIUTIL(%IOCAP("ROWS")-1,I) W BOX("H")
 D LOCATE^MTUIUTIL(3,1)
 Q
;
; BA STRUCTURE
;
;  BA("TOP")=n
;  BA("LEFT")=n
;  BA("RIGHT")=n
;  BA("BOTTOM")=n
;  BA("TITLE")="text"
;  BA("LINES",n)="text"
ALERTBOX(BA)
 S BA("SCALEWIDTH")=BA("RIGHT")-BA("LEFT")-1
 S BA("SCALEHEIGHT")=BA("BOTTOM")-BA("TOP")-4
 S BA("CONTENTTOP")=BA("TOP")+3
 S BA("CONTENTLEFT")=BA("LEFT")+1
 S BA("CAPTIONTOP")=BA("TOP")+1
 S BA("CAPTIONLEFT")=BA("LEFT")+1
 D SETCOLOR^MTUIUTIL("GREEN","BLACK")
 D LOCATE^MTUIUTIL(BA("TOP"),BA("LEFT"))
 W BOX("UL")
 F I=BA("LEFT")+1:1:BA("RIGHT")-1  D
 . D LOCATE^MTUIUTIL(BA("TOP"),I) W BOX("H")
 W BOX("UR")
 D LOCATE^MTUIUTIL(BA("TOP")+1,BA("LEFT")) W BOX("V")
 D LOCATE^MTUIUTIL(BA("TOP")+1,BA("RIGHT")) W BOX("V")
 D LOCATE^MTUIUTIL(BA("TOP")+2,BA("LEFT")) W BOX("LT")
 D LOCATE^MTUIUTIL(BA("TOP")+2,BA("RIGHT")) W BOX("RT")
 F I=BA("LEFT")+1:1:BA("RIGHT")-1  D
 . D LOCATE^MTUIUTIL(BA("TOP")+2,I) W BOX("H")
 D LOCATE^MTUIUTIL(BA("BOTTOM"),BA("LEFT")) W BOX("LL")
 D LOCATE^MTUIUTIL(BA("BOTTOM"),BA("RIGHT")) W BOX("LR")
 F I=BA("LEFT")+1:1:BA("RIGHT")-1  D
 . D LOCATE^MTUIUTIL(BA("BOTTOM"),I) W BOX("H")
 F ROW=BA("TOP")+3:1:BA("BOTTOM")-1  D
 . D LOCATE^MTUIUTIL(ROW,BA("LEFT")) W BOX("V")
 . D LOCATE^MTUIUTIL(ROW,BA("RIGHT")) W BOX("V")
 D LOCATE^MTUIUTIL(BA("CAPTIONTOP"),BA("CAPTIONLEFT"))
 D SETATTR^MTUIUTIL("REVERSE")
 F I=BA("CAPTIONLEFT"):1:BA("RIGHT")-1  D
 . D LOCATE^MTUIUTIL(BA("CAPTIONTOP"),I) W " "
 D LOCATE^MTUIUTIL(BA("CAPTIONTOP"),BA("CAPTIONLEFT")) W BA("TITLE")
 D SETATTR^MTUIUTIL("RESET"),SETCOLOR^MTUIUTIL("GREEN","BLACK")
 F I=BA("CONTENTTOP"):1:BA("BOTTOM")-1  D
 . D LOCATE^MTUIUTIL(I,BA("CONTENTLEFT"))
 . F J=1:1:BA("SCALEWIDTH") W " "
 N CL,LINE S CL=BA("CONTENTTOP"),LINE=""
 F  S LINE=$O(BA("LINES",LINE)) Q:LINE=""  D
 . D LOCATE^MTUIUTIL(CL,BA("CONTENTLEFT"))
 . W $E(BA("LINES",LINE),1,BA("SCALEWIDTH"))
 . S CL=CL+1
 N BR S BR=((BA("SCALEWIDTH")/2)-5)+BA("LEFT")+1
 S BR=$P(BR,".",1)
 D LOCATE^MTUIUTIL(BA("BOTTOM")-2,BR)
 W SHADOW D SETATTR^MTUIUTIL("REVERSE") W "   OK   "
 D SETATTR^MTUIUTIL("RESET"),SETCOLOR^MTUIUTIL("GREEN","BLACK") W SHADOW
 D HOME^MTUIUTIL
 R *TMP
 Q
 ;;
MSGBOX(PROMPT,BUTTONS,TITLE)
 N TOP,LEFT,RIGHT,BOTTOM,BA,WIDTH
 S TOP=((24/2)-(11/2))
 S TOP=$P(TOP,".",1)
 S WIDTH=$L(PROMPT)+4,LEFT=(80/2)-(WIDTH/2)
 S WIDTH=$P(WIDTH,".",1)
 S LEFT=$P(LEFT,".",1)
 S RIGHT=LEFT+WIDTH
 S RIGHT=$P(RIGHT,".",1)
 S BOTTOM=TOP+11
 S BA("TOP")=TOP,BA("LEFT")=LEFT,BA("RIGHT")=RIGHT
 S BA("BOTTOM")=BOTTOM
 S BA("TITLE")=TITLE
 S BA("LINES",1)="",BA("LINES",2)=" "_PROMPT
 S BA("LINES",3)=""
 D ALERTBOX(.BA)
 Q
 ;;
CAPTION(CAPTEXT)
 N CAPLEFT
 S CAPLEFT=(%IOCAP("COLUMNS")/2)-($L(CAPTEXT)/2)
 S CAPLEFT=$P(CAPLEFT,".",1)
 D LOCATE^MTUIUTIL(1,CAPLEFT)
 D SETCOLOR^MTUIUTIL("WHITE","BLACK"),SETATTR^MTUIUTIL("BRIGHT")
 W CAPTEXT
 D SETATTR^MTUIUTIL("RESET")
 Q
 ;;
CONFIRM(CAPTION)
 N KEY,EV,CAPLEFT,CURSEL,QUITFLG
 S CURSEL="N",QUITFLG=0
CONFREDISP
 D LOCATE^MTUIUTIL(%IOCAP("ROWS"),1)
 D ERASETOEOL^MTUIUTIL
 D SETCOLOR^MTUIUTIL("MAGENTA","BLACK")
 D SETATTR^MTUIUTIL("BRIGHT")
 D LOCATE^MTUIUTIL(%IOCAP("ROWS"),1) W CAPTION
 D LOCATE^MTUIUTIL(%IOCAP("ROWS"),%IOCAP("COLUMNS")-20)
 I CURSEL="Y" D 
 . D SETATTR^MTUIUTIL("REVERSE")
 . D SETCOLOR^MTUIUTIL("MAGENTA","BLACK")
 . D SETATTR^MTUIUTIL("BRIGHT")
 E  D 
 . D SETATTR^MTUIUTIL("RESET")
 . D SETCOLOR^MTUIUTIL("MAGENTA","BLACK")
 . D SETATTR^MTUIUTIL("BRIGHT")
 D LOCATE^MTUIUTIL(%IOCAP("ROWS"),%IOCAP("COLUMNS")-20) W "YES"
 I CURSEL="N" D 
 . D SETATTR^MTUIUTIL("REVERSE")
 . D SETCOLOR^MTUIUTIL("MAGENTA","BLACK")
 . D SETATTR^MTUIUTIL("BRIGHT")
 E  D 
 . D SETATTR^MTUIUTIL("RESET")
 . D SETCOLOR^MTUIUTIL("MAGENTA","BLACK")
 . D SETATTR^MTUIUTIL("BRIGHT")
 D LOCATE^MTUIUTIL(%IOCAP("ROWS"),%IOCAP("COLUMNS")-10) W "NO"
 D SETATTR^MTUIUTIL("RESET"),SETCOLOR^MTUIUTIL("MAGENTA","BLACK")
 D SETATTR^MTUIUTIL("BRIGHT")
 D HOME^MTUIUTIL
 S KEY=$$GETCH^%TERMIO(.EV),QUITFLG=0
 I KEY="KEY_LEFT" D
 . I CURSEL="N" S CURSEL="Y",QUITFLG=0
 I KEY="KEY_RIGHT" D
 . I CURSEL="Y" S CURSEL="N",QUITFLG=0
 I KEY="KEY_ENTER" S QUITFLG=1
 G:QUITFLG=0 CONFREDISP
 Q CURSEL