MTUIFORM
 Q
 ;;
;
; MTUIFORM data structure
;
;  TF("TITLE")="TITLE"
;  TF("ITEMS",n,"NAME")="field name"
;  TF("ITEMS",n,"LABEL")="Field label"
;  TF("ITEMS",n,"LABEL","TOP")=n
;  TF("ITEMS",n,"LABEL","LEFT")=n
;  TF("ITEMS",n,"FIELD","ONFOCUS")="CODE"
;  TF("ITEMS",n,"FIELD","ONBLUR")="CODE"
;  TF("ITEMS",n,"FIELD","MAXLENGTH")=n
;  TF("ITEMS",n,"FIELD","TOP")=n
;  TF("ITEMS",n,"FIELD","LEFT")=n
;  TF("ITEMS",n,"FIELD","REQUIRED")=1
;  TF("ITEMS",n,"FIELD","VALUE")="initial value"
;
GO(FA,CAPTION)
 N ITEM S ITEM=""
 N NAME,LABEL,LLEFT,LTOP,ONFOCUS,ONBLUR
 N MAXLENGTH,TOP,LEFT,REQUIRED,VALUE,CURRENT,COUNT
 S COUNT=0
 F  S ITEM=$O(FA("ITEMS",ITEM)) Q:ITEM=""  D
 . I ITEM>COUNT S COUNT=ITEM
 S ITEM=""
 S CURRENT=1
 S DUMMYFLG=0
 S SUBFLG=0
REDISPLAY 
 G:SUBFLG=1 SUBMIT
 D INIT^MTUI
 U $P:(ESCAPE:TERMINATOR=$C(9,13))
 I $G(CAPTION,"")="" D CAPTION^MTUI(FA("TITLE"))
 I $G(CAPTION,"")'="" D CAPTION^MTUI(CAPTION)
 F  S ITEM=$O(FA("ITEMS",ITEM)) Q:ITEM=""  D
 . S HIDDEN=$G(FA("ITEMS",ITEM,"FIELD","HIDDEN"),0)
 . S LLEFT=FA("ITEMS",ITEM,"LABEL","LEFT")
 . S LTOP=FA("ITEMS",ITEM,"LABEL","TOP")
 . S LEFT=FA("ITEMS",ITEM,"FIELD","LEFT")
 . S TOP=FA("ITEMS",ITEM,"FIELD","TOP")
 . S NAME=FA("ITEMS",ITEM,"NAME")
 . S LABEL=FA("ITEMS",ITEM,"LABEL")
 . S ONFOCUS=$G(FA("ITEMS",ITEM,"FIELD","ONFOCUS"))
 . S ONBLUR=$G(FA("ITEMS",ITEM,"FIELD","ONBLUR"))
 . S MAXLENGTH=$G(FA("ITEMS",ITEM,"FIELD","MAXLENGTH"))
 . S REQUIRED=$G(FA("ITEMS",ITEM,"FIELD","REQUIRED"),0)
 . S VALUE=$G(FA("ITEMS",ITEM,"FIELD","VALUE"),"")
 . S ERROR=$G(FA("ITEMS",ITEM,"FIELD","ERROR"))
 . D SETCOLOR^MTUIUTIL("GREEN","BLACK")
 . D LOCATE^MTUIUTIL(LTOP,LLEFT) W LABEL
 . D LOCATE^MTUIUTIL(TOP,LEFT)
 . D SETCOLOR^MTUIUTIL("BLACK","GREEN")
 . F I=1:1:MAXLENGTH W " "
 . I $G(FA("ITEMS",ITEM,"FIELD","VALUE"))'="" D
 . . D LOCATE^MTUIUTIL(TOP,LEFT)
 . . I HIDDEN=0 W $E(FA("ITEMS",ITEM,"FIELD","VALUE"),1,MAXLENGTH)
 . I REQUIRED D LOCATE^MTUIUTIL(TOP,LEFT+MAXLENGTH) D SETCOLOR^MTUIUTIL("RED","BLACK") W "*"
 . I ERROR'="" D LOCATE^MTUIUTIL(TOP+1,LEFT) D SETCOLOR^MTUIUTIL("RED","BLACK") W ERROR
 . S FA("ITEMS",ITEM,"FIELD","ERROR")=""
 D LOCATE^MTUIUTIL(%IOCAP("ROWS")-1,1),SETCOLOR^MTUIUTIL("GREEN","BLACK")
 F I=1:1:%IOCAP("COLUMNS") W BOX("H")
 D LOCATE^MTUIUTIL(%IOCAP("ROWS"),1),SETCOLOR^MTUIUTIL("MAGENTA","BLACK")
 D SETATTR^MTUIUTIL("BRIGHT")
 W "TAB TO NAVIGATE; F2 SUBMITS "
 I DUMMYFLG=0 S CURRENT=$$INPUT(.FA,CURRENT,COUNT) G REDISPLAY
 I DUMMYFLG=1 S CURRENT=$$DUMMYINPUT(.FA,CURRENT,COUNT) G REDISPLAY
SUBMIT
 N ERRCT S ERRCT=0
 S ITEM=""
 F  S ITEM=$O(FA("ITEMS",ITEM)) Q:ITEM=""  D
 . I $G(FA("ITEMS",ITEM,"FIELD","REQUIRED"))=1 D 
 . . I $L($G(FA("ITEMS",ITEM,"FIELD","VALUE")))=0 D
 . . . S ERRCT=ERRCT+1
 . . . S FA("ITEMS",ITEM,"FIELD","ERROR")="INPUT REQUIRED"
 I ERRCT>0 S SUBFLG=0,CURRENT=1 G REDISPLAY
 N MA
 S MA("TITLE")="CONFIRM SUBMISSION"
 S MA("ITEMS","A. SUBMIT FORM")="S (SUBFLG,CANFLG)=0,RETRFLG=0"
 S MA("ITEMS","B. EDIT FORM")="S (SUBFLG,CANFLG)=0,RETRFLG=1"
 S MA("ITEMS","C. CANCEL")="S CANFLG=1,(SUBFLG,RETRFLG)=0"
 D GO^MTUIMENU(.MA)
 G:RETRFLG=1 REDISPLAY
 Q
 ;;
INPUT(FA,CURRENT,COUNT)
 N RETVAL,TMP
 I $G(FA("ITEMS",CURRENT,"FIELD","HIDDEN"))=1 U $P:NOECHO E  U $P:ECHO
 U $P:(ESCAPE:TERMINATOR=$C(9,13))
 D LOCATE^MTUIUTIL(FA("ITEMS",CURRENT,"FIELD","TOP"),FA("ITEMS",CURRENT,"FIELD","LEFT"))
 D SETCOLOR^MTUIUTIL("BLACK","GREEN")
 I $G(FA("ITEMS",CURRENT,"FIELD","PRESERVECASE"),0)=1 D
 . U $P:NOCONVERT
 E  D
 . U $P:CONVERT
 I $G(FA("ITEMS",CURRENT,"FIELD","ONFOCUS"))="" D
 . R TMP#FA("ITEMS",CURRENT,"FIELD","MAXLENGTH") S ZB=$ZB U $P:ECHO
 . I $G(%IOKB(ZB))="KEY_F2" S SUBFLG=1
 . I $G(%IOKB(ZB))'="KEY_F2" S SUBFLG=0
 . I TMP'="" S FA("ITEMS",CURRENT,"FIELD","VALUE")=TMP
 . S DUMMYFLG=0
 I $G(FA("ITEMS",CURRENT,"FIELD","ONFOCUS"))'="" D 
 . X FA("ITEMS",CURRENT,"FIELD","ONFOCUS")
 . S SUBFLG=0
 . S FA("ITEMS",CURRENT,"FIELD","VALUE")=EXTVAL
 . S FA("ITEMS",CURRENT,"FIELD","INTVAL")=INTVAL
 . S CURRENT=CURRENT-1
 . S DUMMYFLG=1
 I CURRENT=COUNT S RETVAL=1
 I CURRENT<COUNT S RETVAL=CURRENT+1
 Q RETVAL
 ;;
DUMMYINPUT(FA,CURRENT,COUNT)
 N RETVAL,TMP,NEXTFLG S NEXTFLG=0
 D LOCATE^MTUIUTIL(FA("ITEMS",CURRENT,"FIELD","TOP"),FA("ITEMS",CURRENT,"FIELD","LEFT"))
 D SETCOLOR^MTUIUTIL("BLACK","GREEN")
 R *TMP S ZB=$ZB
 I $G(%IOKB(ZB))="KEY_F2" S SUBFLG=1
 I $G(%IOKB(ZB))'="KEY_F2" S SUBFLG=0
 I CURRENT=COUNT S RETVAL=1
 I CURRENT<COUNT S RETVAL=CURRENT+1
 S DUMMYFLG=0
 Q RETVAL
 ;;
GETEXTVAL(FA,FIELDNAME)
 N ITEM,RETVAL S (ITEM,RETVAL)=""
 F  S ITEM=$O(FA("ITEMS",ITEM)) Q:ITEM=""  D
 . I FA("ITEMS",ITEM,"NAME")=FIELDNAME S RETVAL=$G(FA("ITEMS",ITEM,"FIELD","VALUE"))
 Q RETVAL
 ;;
GETINTVAL(FA,FIELDNAME)
 N ITEM,RETVAL S (ITEM,RETVAL)=""
 F  S ITEM=$O(FA("ITEMS",ITEM)) Q:ITEM=""  D
 . I FA("ITEMS",ITEM,"NAME")=FIELDNAME S RETVAL=$G(FA("ITEMS",ITEM,"FIELD","INTVAL"))
 Q RETVAL